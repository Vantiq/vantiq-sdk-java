buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1+'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

//
// Project Settings
//
group            = 'io.vantiq'
archivesBaseName = 'vantiq-sdk'
version          = '1.0.0'

allprojects {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.google.code.gson:gson:2.6.2'

    testCompile 'com.squareup.okhttp:mockwebserver:2.7.5'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

test {
    useJUnit {
        excludeCategories 'io.vantiq.client.intg.IntegrationTests'
    }
}

task intgTest(type:Test) {
    systemProperties['server']   = System.getProperty('server')
    systemProperties['username'] = System.getProperty('username')
    systemProperties['password'] = System.getProperty('password')
    useJUnit {
        includeCategories 'io.vantiq.client.intg.IntegrationTests'
    }
}

//
// Ensure we create sources and javadoc JAR files for publishing
//
task sourcesJar(type:Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type:Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

//
// jCenter publishing support
//
bintray {

    user = System.getenv('BINTRAY_USER')
    key  = System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']

    publish = false
    dryRun = false

    pkg {
        repo = 'generic'
        name = archivesBaseName
        desc = 'Vantiq SDK for Java and Android'

        websiteUrl      = 'https://github.com/Vantiq/vantiq-sdk-java'
        issueTrackerUrl = 'https://github.com/Vantiq/vantiq-sdk-java/issues'
        vcsUrl          = 'https://github.com/Vantiq/vantiq-sdk-java.git'
        version {
            vcsTag = '1.0.0'
        }

        licenses = ['MIT']

        githubRepo = 'Vantiq/vantiq-sdk-java'
        githubReleaseNotesFile = 'README.md'
    }
}
